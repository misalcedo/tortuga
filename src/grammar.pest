Program    = { SOI ~ (Expression{2, } | Comparison) ~ EOI }
Comparison = { Expression ~ ( INEQUALITY ~ Expression )* }
Expression = { Match }

Match       = { ( Pattern ~ "=" )? ~ Block }
Pattern     = { Name ~ ( Numerical | Parameters )? | Destructure }
Name        = { "_" | IDENTIFIER }
Numerical   = { "." ~ ( "0" | Name ) }
Destructure = { "{" ~ Parameter ~ ( "," ~ Parameter )* ~ ( "|" ~ Name )? ~ "}" }
Parameters  = { "(" ~ Parameter ~ ( "," ~ Parameter )* ~ ")" }
Parameter   = { Name ~ ( Numerical | Refinement | Parameters )? | Destructure }
Refinement  = { ( INEQUALITY | "=" ) ~ Arithmetic }

Block      = { "[" ~ Expression+ ~ "]" | Arithmetic }
Arithmetic = { Epsilon }
Epsilon    = { Modulo ~ ( EPSILON ~ Modulo )? }
Modulo     = { Sum ~ ( MODULO ~ Sum )* }
Sum        = { Product ~ ( SUM ~ Product )* }
Product    = { Power ~ ( PRODUCT ~ Power )* }
Power      = { Call ~ ( POWER ~ Call )* }


Call       = { Terminal ~ Arguments* }
Arguments  = { "(" ~ Expression ~ ( "," ~ Expression )* ~ ")" }

Terminal   = _{ Number | IDENTIFIER | Tuple | Range | Grouping }
Tuple      = { "{" ~ ( Terminal ~ ( "," ~ Terminal )* )? ~ "}" }
Grouping   = { "(" ~ Expression ~ ")" }
Number     = { ( "-" ~ !ZERO )? ~ ( RADIX ~ REAL_BASE_36 | REAL ) }
Range      = { ( "(" | "[" ) ~ Arithmetic ~ "," ~ Arithmetic ~ ( ")" | "]" ) }
IDENTIFIER = @{ XID_START ~ XID_CONTINUE* }

ZERO         = @{ "0" | "0.0" | "0." | ".0" }
RADIX        = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT? ~ "#" }
REAL         = @{ ASCII_DIGIT+ ~ ( "." ~ ASCII_DIGIT* )? | "." ~ ASCII_DIGIT+ }
REAL_BASE_36 = @{ ASCII_ALPHANUMERIC+ ~ ( "." ~ ASCII_ALPHANUMERIC* )? | "." ~ ASCII_ALPHANUMERIC+ }

EPSILON    = { "~" }
MODULO     = { "%" }
SUM        = { "+" | "-" }
PRODUCT    = { "*" | "/" }
POWER      = { "^" }
INEQUALITY = { "<>" | "<=" | ">=" | "<" | ">" }
EQUALITY   = { "=" }

WHITESPACE = _{ PATTERN_WHITE_SPACE }
COMMENT    = _{ ";" ~ (!NEWLINE ~ ANY)* }