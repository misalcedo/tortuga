Program    = { SOI ~ Expression* ~ EOI }

Expression = { Inequality }
Inequality = { Equality ~ ( INEQUALITY ~ Equality )* }
Equality   = { Arithmetic ~ ( EQUALITY ~ Block )? }
Block      = { Equality | ( "[" ~ Equality ~ Equality+ ~ "]" ) }

Arithmetic = { Modulo }
Modulo     = { Sum ~ ( MODULO ~ Sum )* }
Sum        = { Product ~ ( SUM ~ Product )* }
Product    = { Power ~ ( PRODUCT ~ Power )* }
Power      = { Primary ~ ( POWER ~ Primary )* }

Primary    = _{ Number | Call | Grouping }
Number     = { QUISRADIX | (SIGN? ~ DECIMAL) }
Call       = { IDENTIFIER ~ ( "(" ~ Arguments ~ ")" )? }
Grouping   = { "(" ~ Expression ~ ")" }

Arguments  = { Expression ~ ( "," ~ Expression )* }
Parameters = { Pattern ~ ( "," ~ Pattern )* }

Pattern    = { Range | Function }
Function   = { IDENTIFIER ~ ( "(" ~ Parameters ~ ")" )? }
Range      = { ( Expression ~ INEQUALITY )? ~ IDENTIFIER ~ ( INEQUALITY ~ Expression )? }

IDENTIFIER = { ( ANONYMOUS ~ !( LETTER | NUMBER ) ) | ( !(NUMBER | ANONYMOUS) ~ CHARACTER+ ~ !ANONYMOUS ) }
CHARACTER  = _{ ANONYMOUS | LETTER | NUMBER }
ANONYMOUS  = _{ "_" }

QUISRADIX  = ${ RADIX ~ "#" ~ SIGN? ~ BASE36 }
RADIX      = @{ "i" | ASCII_DIGIT+ }
DECIMAL    = @{ (ASCII_DIGIT+ ~ ( "." ~ ASCII_DIGIT* )? | ( "." ~ ASCII_DIGIT+ ) ) ~ !IDENTIFIER }
BASE36     = @{ ASCII_ALPHANUMERIC+ ~ ( "." ~ ASCII_ALPHANUMERIC* )? | ( "." ~ ASCII_ALPHANUMERIC+ ) }
SIGN       = @{ SUM }

MODULO     = { "%" }
SUM        = { "+" | "-" }
PRODUCT    = { "*" | "/" }
POWER      = { "^" }
INEQUALITY = { "<>" | "<=" | ">=" | "<" | ">" }
EQUALITY   = { "=" }

WHITESPACE = _{ SPACE_SEPARATOR | NEWLINE }
COMMENT    = _{ ";" ~ (!NEWLINE ~ ANY)* }
